PLEASE NOTE: IACC FILE TO GENERATE A COMPILER COMPATIBLE WITH C-INTERCAL
                                              (HOPEFULLY)
Copyright (c) 2006-2008, 2023 Claudio Calvelli, all rights reserved.

CLC-INTERCAL is copyrighted software. However, permission to use, modify,
and distribute it is granted provided that the conditions set out in the
licence agreement are met. See files README and COPYING in the distribution.

PERVERSION "CLC-INTERCAL/Base INTERCAL/Include/ick.iacc 1.-94.-2.2"

	DO %BA <- #2
	DO %RT <- ?TRADITIONAL
	DO %WT <- #0
	DO %CR <- ?ASCII
	DO %CW <- ?ASCII
	DO %IO <- ?C
	DO %CF <- #0
	DO %RM <- #0
	DO %SM <- #0
	DO %GU <- #1
	DO ?TYPE <- ?COMPILER
	DO CREATE _2 ?PROGRAM ?STATEMENT AS ?STATEMENT #1
	DO CREATE _2 ?CALC_EXPR ?EXPRESSION AS STS + * + ROU + #1 +
	   ?EXPRESSION #1
	DO CREATE _2 ?CALC_EXPR ?ARRAY AS STS + * + ROU + #1 + ?ARRAY
	   #1
	DO CREATE _2 ?CALC_EXPR ?CALCULATION AS STS + * + ?CALCULATION
	   #1
	DO CREATE _2 ?CALC_FULL ?EXPRESSION AS STS + * + ROU + #1 +
	   ?EXPRESSION #1
	DO CREATE _2 ?CALC_FULL ?ARRAY AS STS + * + ROU + #1 + ?ARRAY
	   #1
	DO CREATE _2 ?CALC_FULL ?STATEMENT AS ?STATEMENT #1
	DO CREATE _2 ?END_JUNK ?STMT_LABEL AS ,,
	DO CREATE _2 ?END_JUNK ?DO_PLEASE AS ,,
	DO CREATE _2 ?STATEMENT ?MAYBE_LABEL ?DO_PLEASE ?QUALIFIERS
	   ?VERB ?MAYBE_ONCE AS STS + * + ?MAYBE_LABEL #1 + ?DO_PLEASE
	   #1 + ?QUALIFIERS #1 + ?MAYBE_ONCE #1 + ?VERB #1
	DO CREATE _2 ?STATEMENT ?STMT_LABEL AS STS + * + LAB +
	   ?STMT_LABEL #1
	DO CREATE _2 ?MAYBE_LABEL ,, AS ,,
	DO CREATE _2 ?MAYBE_LABEL ?STMT_LABEL AS LAB + ?STMT_LABEL #1
	DO CREATE _2 ?DO_PLEASE ,DO, AS ,,
	DO CREATE _2 ?DO_PLEASE ,PLEASE, ?MAYBE_DO AS BIT + #5
	DO CREATE _2 ?MAYBE_DO ,, AS ,,
	DO CREATE _2 ?MAYBE_DO ,DO, AS ,,
	DO CREATE _2 ?QUALIFIERS ,, AS ,,
	DO CREATE _2 ?QUALIFIERS ,#37, ?CONSTANT ?QUALIFIERS AS DSX +
	   ?CONSTANT #1 + ?QUALIFIERS #1
	DO CREATE _2 ?QUALIFIERS ,NOT, ?QUALIFIERS AS NOT + ?QUALIFIERS
	   #1
	DO CREATE _2 ?QUALIFIERS ,N, ,#39, ,T, ?QUALIFIERS AS NOT +
	   ?QUALIFIERS #1
	DO CREATE _2 ?MAYBE_ONCE ,, AS ,,
	DO CREATE _2 ?MAYBE_ONCE ,ONCE, AS BIT + #3
	DO CREATE _2 ?MAYBE_ONCE ,AGAIN, AS BIT + #4
	DO CREATE _2 ?VERB ,NOT, ?JUNK AS NOT + MSP + #0 + #1 + ?JUNK
	   #1
	DO CREATE _2 ?VERB ,N, ,#39, ,T, ?JUNK AS NOT + MSP + #0 + #1 +
	   ?JUNK #1
	DO CREATE _2 ?CALCULATION ?LVALUE ,#60, ,#45, ?EXPRESSION AS
	   STO + ?EXPRESSION #1 + ?LVALUE #1
	DO CREATE _2 ?CALCULATION ?ARRAY ,#60, ,#45, ?DIMENSION AS STO
	   + MUL + !DIMENSION #1 + ?DIMENSION #1 + ?ARRAY #1
	DO CREATE _2 ?VERB ?CALCULATION AS ?CALCULATION #1
	DO CREATE _2 ?GERUND ,CALCULATING,=1 AS STO
	DO CREATE _2 ?VERB ?ICK_LABEL ,NEXT, AS NXT + ?ICK_LABEL #1
	DO CREATE _2 ?GERUND ,NEXTING,=1 AS NXT
	DO CREATE _2 ?VERB ,RESUME, ?EXPRESSION AS RES + ?EXPRESSION #1
	DO CREATE _2 ?GERUND ,RESUMING,=1 AS RES
	DO CREATE _2 ?VERB ,FORGET, ?EXPRESSION AS FOR + ?EXPRESSION #1
	DO CREATE _2 ?GERUND ,FORGETTING,=1 AS FOR
	DO CREATE _2 ?VERB ,STASH, ?STASHNAMES AS STA + !STASHNAMES #1
	   + ?STASHNAMES #1
	DO CREATE _2 ?GERUND ,STASHING,=1 AS STA
	DO CREATE _2 ?VERB ,RETRIEVE, ?NAMES AS RET + !NAMES #1 +
	   ?NAMES #1
	DO CREATE _2 ?GERUND ,RETRIEVING,=1 AS RET
	DO CREATE _2 ?VERB ,IGNORE, ?NAMES AS IGN + !NAMES #1 + ?NAMES
	   #1
	DO CREATE _2 ?GERUND ,IGNORING,=1 AS IGN
	DO CREATE _2 ?VERB ,REMEMBER, ?NAMES AS REM + !NAMES #1 +
	   ?NAMES #1
	DO CREATE _2 ?GERUND ,REMEMBERING,=1 AS REM
	DO CREATE _2 ?VERB ,ABSTAIN, ,FROM, ?ABSTAIN AS ?ABSTAIN #1
	DO CREATE _2 ?ABSTAIN ?ICK_LABEL AS ABL + ?ICK_LABEL #1
	DO CREATE _2 ?ABSTAIN ?GERUNDS AS ABG + !GERUNDS #1 + ?GERUNDS
	   #1
	DO CREATE _2 ?GERUND ,ABSTAINING,=2 AS ABL + ABG
	DO CREATE _2 ?VERB ,REINSTATE, ?REINSTATE AS ?REINSTATE #1
	DO CREATE _2 ?REINSTATE ?ICK_LABEL AS REL + ?ICK_LABEL #1
	DO CREATE _2 ?REINSTATE ?GERUNDS AS REG + !GERUNDS #1 +
	   ?GERUNDS #1
	DO CREATE _2 ?GERUND ,REINSTATING,=2 AS REL + REG
	DO CREATE _2 ?GERUNDS ?GERUND=* AS ?GERUND #1
	DO CREATE _2 ?GERUNDS ?GERUND=* ,#43, ?GERUNDS=* AS ?GERUND #1
	   + ?GERUNDS #1
	DO CREATE _2 ?VERB ,GIVE, ,UP, AS GUP
	DO CREATE _2 ?VERB ,WRITE, ,IN, ?WVALUES AS WIN + !WVALUES #1 +
	   ?WVALUES #1
	DO CREATE _2 ?GERUND ,WRITING, ,IN,=1 AS WIN
	DO CREATE _2 ?VERB ,READ, ,OUT, ?RVALUES AS ROU + !RVALUES #1 +
	   ?RVALUES #1
	DO CREATE _2 ?GERUND ,READING, ,OUT,=1 AS ROU
	DO CREATE _2 ?VERB ,COME, ,FROM, ?ICK_LABEL AS CFL + ?ICK_LABEL
	   #1
	DO CREATE _2 ?GERUND ,COMING, ,FROM,=1 AS CFL
	DO CREATE _2 ?NAME ?RNAME AS ?RNAME #1
	DO CREATE _2 ?NAME ?ARRAY AS ?ARRAY #1
	DO CREATE _2 ?NAMES ?NAME=1 AS ?NAME #1
	DO CREATE _2 ?NAMES ?NAME=1 ,#43, ?NAMES=* AS ?NAME #1 + ?NAMES
	   #1
	DO CREATE _2 ?STASHNAMES ?NAME=1 AS ?NAME #1
	DO CREATE _2 ?STASHNAMES ?NAME=1 ,#43, ?STASHNAMES=* AS ?NAME
	   #1 + ?STASHNAMES #1
	DO CREATE _2 ?LVALUE ?RNAME AS ?RNAME #1
	DO CREATE _2 ?LVALUE ?LELEMENT AS ?LELEMENT #1
	DO CREATE _2 ?RVALUE ?EXPRESSION AS ?EXPRESSION #1
	DO CREATE _2 ?RVALUE ?ARRAY AS ?ARRAY #1
	DO CREATE _2 ?RVALUES ?RVALUE=1 AS ?RVALUE #1
	DO CREATE _2 ?RVALUES ?RVALUE=1 ,#43, ?RVALUES=* AS ?RVALUE #1
	   + ?RVALUES #1
	DO CREATE _2 ?WVALUE ?RNAME AS ?RNAME #1
	DO CREATE _2 ?WVALUE ?LELEMENT AS ?LELEMENT #1
	DO CREATE _2 ?WVALUE ?ARRAY AS ?ARRAY #1
	DO CREATE _2 ?WVALUES ?WVALUE=1 AS ?WVALUE #1
	DO CREATE _2 ?WVALUES ?WVALUE=1 ,#43, ?WVALUES=* AS ?WVALUE #1
	   + ?WVALUES #1
	DO CREATE _2 ?REGISTER ,#46, ?UNARIES ?CONSTANT AS ?UNARIES #1
	   + SPO + ?CONSTANT #1
	DO CREATE _2 ?REGISTER ,#58, ?UNARIES ?CONSTANT AS ?UNARIES #1
	   + TSP + ?CONSTANT #1
	DO CREATE _2 ?RNAME ,#46, ?CONSTANT AS SPO + ?CONSTANT #1
	DO CREATE _2 ?RNAME ,#58, ?CONSTANT AS TSP + ?CONSTANT #1
	DO CREATE _2 ?ARRAY ,#44, ?CONSTANT AS TAI + ?CONSTANT #1
	DO CREATE _2 ?ARRAY ,#59, ?CONSTANT AS HYB + ?CONSTANT #1
	DO CREATE _2 ?ELEMENT ,#44, ?UNARIES ?CONSTANT ?SUBSCRIPTS AS
	   ?UNARIES #1 + ?SUBSCRIPTS #1 + TAI + ?CONSTANT #1
	DO CREATE _2 ?ELEMENT ,#59, ?UNARIES ?CONSTANT ?SUBSCRIPTS AS
	   ?UNARIES #1 + ?SUBSCRIPTS #1 + HYB + ?CONSTANT #1
	DO CREATE _2 ?LELEMENT ,#44, ?CONSTANT ?SUBSCRIPTS AS
	   ?SUBSCRIPTS #1 + TAI + ?CONSTANT #1
	DO CREATE _2 ?LELEMENT ,#59, ?CONSTANT ?SUBSCRIPTS AS
	   ?SUBSCRIPTS #1 + HYB + ?CONSTANT #1
	DO CREATE _2 ?SUBSCRIPTS ,SUB, ?EXPRESSION ?MORE_SUBSCRIPTS AS
	   ?MORE_SUBSCRIPTS #1 + SUB + ?EXPRESSION #1
	DO CREATE _2 ?MORE_SUBSCRIPTS ,, AS ,,
	DO CREATE _2 ?MORE_SUBSCRIPTS ?EXPRESSION ?MORE_SUBSCRIPTS AS
	   ?MORE_SUBSCRIPTS #1 + SUB + ?EXPRESSION #1
	DO CREATE _2 ?NONUNARIES ?REGISTER AS ?REGISTER #1
	DO CREATE _2 ?NONUNARIES ?ELEMENT AS ?ELEMENT #1
	DO CREATE _2 ?NONUNARIES ,#35, ?UNARIES ?CONSTANT AS ?UNARIES
	   #1 + ?CONSTANT #1
	DO CREATE _2 ?NONUNARIES ,#39, ?UNARIES ?EXPRESSION ,#39, AS
	   ?UNARIES #1 + ?EXPRESSION #1
	DO CREATE _2 ?NONUNARIES ,#34, ?UNARIES ?EXPRESSION ,#34, AS
	   ?UNARIES #1 + ?EXPRESSION #1
	DO CREATE _2 ?BANG ,#33, ?UNARIES ?CONSTANT AS ?UNARIES #1 +
	   SPO + ?CONSTANT #1
	DO CREATE _2 ?NONUNARIES ?BANG ?MAYBE_BINARY ,#39, AS
	   ?MAYBE_BINARY #1 + ?BANG #1
	DO CREATE _2 ?RABBIT ,#34 + #8 + #46, ?UNARIES ?CONSTANT AS
	   ?UNARIES #1 + SPO + ?CONSTANT #1
	DO CREATE _2 ?NONUNARIES ?RABBIT ?MAYBE_BINARY ,#34, AS
	   ?MAYBE_BINARY #1 + ?RABBIT #1
	DO CREATE _2 ?EXPRESSION ?NONUNARIES ?MAYBE_BINARY AS
	   ?MAYBE_BINARY #1 + ?NONUNARIES #1
	DO CREATE _2 ?UNARIES ,, AS ,,
	DO CREATE _2 ?UNARIES ?UNARY ?UNARIES AS ?UNARY #1 + ?UNARIES
	   #1
	DO CREATE _2 ?UNARY ,V, AS BUT + #7
	DO CREATE _2 ?UNARY ,#38, AS BUT + #0
	DO CREATE _2 ?UNARY ,#86 + #8 + #45, AS SWB
	DO CREATE _2 ?UNARY ,#63, AS SWB
	DO CREATE _2 ?UNARY ,#64, AS BUT + #1
	DO CREATE _2 ?UNARY ,2, ,#64, AS BUT + #2
	DO CREATE _2 ?UNARY ,3, ,#64, AS BUT + #3
	DO CREATE _2 ?UNARY ,4, ,#64, AS BUT + #4
	DO CREATE _2 ?UNARY ,5, ,#64, AS BUT + #5
	DO CREATE _2 ?UNARY ,#94, AS AWC
	DO CREATE _2 ?MAYBE_BINARY ,, AS ,,
	DO CREATE _2 ?MAYBE_BINARY ?BINARY ?EXPRESSION AS ?BINARY #1 +
	   ?EXPRESSION #1
	DO CREATE _2 ?BINARY ,#126, AS RSE
	DO CREATE _2 ?BINARY ,#67 + #8 + #47, AS RIN
	DO CREATE _2 ?BINARY ,#67 + #8 + #124, AS RIN
	DO CREATE _2 ?BINARY ,#162, AS RIN
	DO CREATE _2 ?BINARY ,#36, AS RIN
	DO CREATE _2 ?BINARY ,#165, AS RIN
	DO CREATE _2 ?BINARY ,#163, AS RIN
	DO CREATE _2 ?DIMENSION ?EXPRESSION=1 AS ?EXPRESSION #1
	DO CREATE _2 ?DIMENSION ?EXPRESSION=1 ,BY, ?DIMENSION=* AS
	   ?EXPRESSION #1 + ?DIMENSION #1
	DO CREATE _2 ?STMT_LABEL ,#40, ?CONSTANT ,#41, AS ?CONSTANT #1
	DO CREATE _2 ?ICK_LABEL ,#40, ?CONSTANT ,#41, AS ?CONSTANT #1

	DO GIVE UP
