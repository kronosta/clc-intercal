PLEASE NOTE: IACC FILE TO ADD INTERNET SUPPORT TO SICK

Copyright (c) 2006-2008, 2023 Claudio Calvelli, all rights reserved.

CLC-INTERCAL is copyrighted software. However, permission to use, modify,
and distribute it is granted provided that the conditions set out in the
licence agreement are met. See files README and COPYING in the distribution.

PERVERSION "CLC-INTERCAL/INET INTERCAL/Include/internet.iacc 1.-94.-2.1"

	PLEASE NOTE: OUR NEW REGISTERS
	DO CREATE _2 ?REG69 ,TH,=2 AS DOS + #20

	PLEASE NOTE: OUR NEW OPCODES
	DO CREATE _2 ?BYTECODE ,CSE,=1 AS :47
	DO CREATE _2 ?BYTECODE ,SMU,=1 AS :46
	DO CREATE _2 ?BYTECODE ,STE,=1 AS :45

	PLEASE NOTE: WE SAY %20 RATHER THAN %TH AS WE HAVEN'T CREATED
	    A RULE FOR %TH IN THE EFFECTIVE GRAMMAR
	DO %20 <- #1
	DO ?TYPE <- ?EXTENSION
	DO ?LOAD_INET <- ?InterNet

	PLEASE NOTE: OUR NEW STATEMENTS
	DO CREATE _2 ?VERB ,STEAL, ?NAMES ?STEAL_ON ?STEAL_FROM
	   ?Q_STEAL AS ?Q_STEAL #1 + :45 + ?STEAL_FROM #1 + ?STEAL_ON
	   #1 + !NAMES #1 + ?NAMES #1
	DO CREATE _2 ?VERB ,SMUGGLE, ?NAMES ?STEAL_ON ?STEAL_FROM
	   ?Q_SMUGGLE AS ?Q_SMUGGLE #1 + :46 + ?STEAL_FROM #1 +
	   ?STEAL_ON #1 + !NAMES #1 + ?NAMES #1
	DO CREATE _2 ?GERUND ,STEALING,=1 AS :45
	DO CREATE _2 ?TEMPLATE ,STEAL, ,REGISTER, ?MAYBE_LIST
	   ?G_STEAL_ON ?G_STEAL_FROM AS :45
	DO CREATE _2 ?GERUND ,SMUGGLING,=1 AS :46
	DO CREATE _2 ?TEMPLATE ,SMUGGLE, ,REGISTER, ?MAYBE_LIST
	   ?G_STEAL_ON ?G_STEAL_FROM AS :46
	DO CREATE _2 ?Q_STEAL ,, AS ,,
	DO CREATE _2 ?Q_STEAL ,WHILE, ,NOT, ,STEALING, ?IT AS QUA
	DO CREATE _2 ?Q_SMUGGLE ,, AS ,,
	DO CREATE _2 ?Q_SMUGGLE ,WHILE, ,NOT, ,SMUGGLING, ?IT AS QUA
	DO CREATE _2 ?STEAL_FROM ,, AS #0
	DO CREATE _2 ?STEAL_FROM ,FROM, ?EXPRESSION AS #1 + ?EXPRESSION
	   #1
	DO CREATE _2 ?STEAL_ON ,, AS #0
	DO CREATE _2 ?STEAL_ON ,ON, ?EXPRESSION AS #1 + ?EXPRESSION #1
	DO CREATE _2 ?G_STEAL_FROM ,, AS ,,
	DO CREATE _2 ?G_STEAL_FROM ,FROM, ,EXPRESSION, AS ,,
	DO CREATE _2 ?G_STEAL_ON ,, AS ,,
	DO CREATE _2 ?G_STEAL_ON ,ON, ,EXPRESSION, AS ,,
	DO CREATE _2 ?VERB ,CASE, ?EXPRESSION ?Q_CASE ,IN,
	   ?CASE_ELEMENTS ?MAYBE_ESAC AS ?Q_CASE #1 + :47 + ?EXPRESSION
	   #1 + !CASE_ELEMENTS #1 + ?CASE_ELEMENTS #1
	DO CREATE _2 ?GERUND ,CASING,=1 AS :47
	DO CREATE _2 ?TEMPLATE ,CASE, ,EXPRESSION, ,IN, ,CASE_ELEMENTS,
	   ?MAYBE_ESAC AS :47
	DO CREATE _2 ?Q_CASE ,, AS ,,
	DO CREATE _2 ?Q_CASE ,WHILE, ,NOT, ,CASING, AS QUA
	DO CREATE _2 ?CASE_ELEMENTS ?REGISTER ,THEN, ?VERB=1 AS
	   ?REGISTER #1 + ?VERB #1
	DO CREATE _2 ?CASE_ELEMENTS ?REGISTER ,THEN, ?VERB=1 ,OR,
	   ?CASE_ELEMENTS=* AS ?REGISTER #1 + ?VERB #1 + ?CASE_ELEMENTS
	   #1
	DO CREATE _2 ?MAYBE_ESAC ,, AS ,,
	DO CREATE _2 ?MAYBE_ESAC ,ESAC, AS ,,

	DO GIVE UP
